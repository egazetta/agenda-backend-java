plugins {
	id 'org.springframework.boot' version '2.3.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
}

group = 'br.com.exemplo'
version = '1.0.0'
sourceCompatibility = '8'

repositories {
	mavenCentral()
}

apply plugin : 'groovy'

sourceSets {
	integration {
		groovy.srcDir "$projectDir/src/integration/groovy"
		resources.srcDir "$projectDir/src/integration/resources"
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}
}

configurations {
	integrationImplementation.extendsFrom testImplementation
	integrationRuntime.extendsFrom testRuntime
}


dependencies {

	// https://mvnrepository.com/artifact/commons-codec/commons-codec
	compile group: 'commons-codec', name: 'commons-codec', version: '1.5'

	// https://mvnrepository.com/artifact/com.github.ulisesbocchio/jasypt-spring-boot-starter
	compile group: 'com.github.ulisesbocchio', name: 'jasypt-spring-boot-starter', version: '2.1.0'

	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'org.springframework.boot:spring-boot-starter-test'

	// https://mvnrepository.com/artifact/com.amazonaws/aws-java-sdk-s3
	implementation group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.845'

	// https://mvnrepository.com/artifact/commons-io/commons-io
	compile group: 'commons-io', name: 'commons-io', version: '2.8.0'

	// https://mvnrepository.com/artifact/org.apache.commons/commons-text
	implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'


	testImplementation("org.spockframework:spock-spring:1.2-groovy-2.5")

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	integrationImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	testRuntime("org.codehaus.groovy:groovy-all:2.5.2")
}
bootRun {
	jvmArgs = ["-Djdk.tls.client.protocols=TLSv1.2", "-Djasypt.encryptor.password=1234"]
}

bootJar {
	mainClassName = 'br.com.exemplo.agenda.AgendaApplication'
	manifest {
		attributes('Implementation-Title':   'Agenda',
				'Implementation-Version': getArchiveVersion())
	}
}

configurations {
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
}

test {
	jvmArgs = ["-Djdk.tls.client.protocols=TLSv1.2"]
	useJUnitPlatform()
}

task integrationTest(type: Test) {
	testClassesDirs = sourceSets.integration.output.classesDirs
	classpath = sourceSets.integration.runtimeClasspath
}

check.dependsOn integrationTest